-- Search every column in every table for a specific string using information schema
-- Information schema is more portable comapared to sys.object and seems to perform better even tho theoritically it shouldn't
-- Narrow the scope so that it runs faster 

--USE DATABASE_NAME 
DECLARE @SearchStr NVARCHAR(100) = 'string'                                        -- edit here for the search string
DECLARE @Results TABLE 
  ( 
     columnname  NVARCHAR(370), 
     columnvalue NVARCHAR(3630) 
  ) 

SET nocount ON 

DECLARE @TableName  NVARCHAR(256), 
        @ColumnName NVARCHAR(128), 
        @SearchStr2 NVARCHAR(110) 

SET @TableName = '' 
SET @SearchStr2 = Quotename('%' + @SearchStr + '%', '''') 

WHILE @TableName IS NOT NULL 
  BEGIN
      SET @ColumnName = '' 
      SET @TableName = (SELECT Min(Quotename(table_schema) + '.' 
                                   + Quotename(table_name)) 
                        FROM   information_schema.tables 
                        WHERE  table_type = 'BASE TABLE' 
								--TABLE_NAME in ()
                               --AND table_name LIKE ( '%work%' )                          --edit here to restrict table name
							   --AND table_name not in ()
                               AND Quotename(table_schema) + '.' 
                                   + Quotename(table_name) > @TableName 
                               AND Objectproperty(Object_id(Quotename( 
                                                            table_schema 
                                                            ) + 
                                                            '.' 
                                                            + Quotename( 
                                                            table_name 
                                                            )), 
                                   'IsMSShipped') = 
                                   0) 

      WHILE ( @TableName IS NOT NULL ) 
            AND ( @ColumnName IS NOT NULL ) 
        BEGIN 
            SET @ColumnName = (SELECT Min(Quotename(column_name)) 
                               FROM   information_schema.columns 
                               WHERE  table_schema = Parsename(@TableName, 2) 
                                      AND table_name = Parsename(@TableName, 1) 
                                      --AND data_type = 'int'                             -- edit here to restrict data type
                                     -- AND column_name LIKE '%Key%'                      -- edit here to restrict column name
                                     -- AND    COLUMN_NAME not in ( )   -- edit here to restrict column name
                                      AND Quotename(column_name) > @ColumnName) 

            IF @ColumnName IS NOT NULL 
              BEGIN 
                  INSERT INTO @Results 
                  EXEC ( 'SELECT ''' + @TableName + '.' + @ColumnName + 
                  ''', LEFT(' + 
                  @ColumnName 
                  + ', 3630)                  FROM ' + @TableName + ' (NOLOCK) ' + 
                  ' WHERE ' 
                  + 
                  @ColumnName + 
                  ' LIKE ' + @SearchStr2 ) 

                  PRINT @TableName + '.' + @ColumnName 
              END 
        END 
  END 

SELECT columnname, 
       columnvalue 
FROM   @Results 
